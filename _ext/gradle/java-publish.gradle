apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

def isSnapshot = ext_version.endsWith('-SNAPSHOT')
// pulls the credentials from either the environment variable or gradle.properties
def cred = {
	if (System.env[it] != null) {
		return System.env[it]
	} else if (project.hasProperty(it)) {
		return project[it]
	} else {
		return 'unknown_' + it
	}
}

model {
	publishing {
		publications {
			mavenJava(MavenPublication) {

				groupId project.ext_group
				artifactId project.ext_artifactId
				version project.ext_version
				from components.java

				pom.withXml {

					// add MavenCentral requirements to the POM
					asNode().children().last() + {
						resolveStrategy = Closure.DELEGATE_FIRST
						name project.ext_artifactId
						description project.ext_description
						url "https://github.com/${project.ext_org}/${project.name}"
						scm {
							url "https://github.com/${project.ext_org}/${project.name}"
							connection "scm:git:git://github.com/${project.ext_org}/${project.name}"
							developerConnection "scm:git:ssh:git@github.com/${project.ext_org}/${project.name}"
						}
						licenses {
							license {
								name 'Eclipse Public License - v 1.0'
								url 'https://www.eclipse.org/legal/epl-v10.html'
								distribution 'repo'
							}
						}
						developers {
							developer {
								id 'fvgh'
								name 'Frank Vennemeyer'
								email 'frankgh@zoho.com'
							}
						}
					}
				}
			}
		}
		if (isSnapshot) {
			// upload snapshots to oss.sonatype.org
			repositories {
				maven {
					url = 'https://oss.sonatype.org/content/repositories/snapshots'
					credentials {
						username = cred('nexus_user')
						password = cred('nexus_pass')
					}
				} }
		}
	}
}

if (!isSnapshot) {
	// upload releases to bintray and then mavenCentral
	bintray {
		user = cred('bintray_user')
		key = cred('bintray_pass')
		publications = [
			'mavenJava'
		]
		publish = true
		pkg {
			repo = 'opensource'
			name = project.ext_artifactId
			userOrg = project.ext_org
			version {
				name = project.ext_version
				mavenCentralSync {
					user = cred('nexus_user')
					password = cred('nexus_pass')
				}
			}
		}
	}

	publish.dependsOn(bintrayUpload)
	bintrayUpload.dependsOn([
		'generatePomFileForMavenJavaPublication',
		jar,
		sourcesJar,
		javadocJar
	])
}
